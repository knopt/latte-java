-- programs ------------------------------------------------

entrypoints Program ;

ProgramTD.   Program ::= [TopDef] ;

FnDef.	   TopDef  ::= Type Ident "(" [Arg] ")" Block ;
ClassDecl.  TopDef ::= ClassHeader "{" [FieldDeclaration] "}";

separator nonempty TopDef "" ;

ArgTI.  Arg ::= Type Ident;

separator  Arg "," ;

-- classes -------------------------------------------------

ClassDec.	ClassHeader ::= "class" Ident Implements;
InterDec.	ClassHeader ::= "interface" Ident;

InterImpl.  Implements ::= "implements" Ident;
EImpl.      Implements ::= "" ;

Dvar.			FieldDeclaration ::= Modifier Type [Item] ";";
Dmth.			FieldDeclaration ::= Modifier Type Ident "(" [Arg] ")" MethodBody ;
terminator  	FieldDeclaration "";


EmptyMBody. MethodBody ::= ";" ;
MBody.      MethodBody ::= Block ;

--- modifiers -----------------------------------------------

Mfinal.		  Modifier ::= "final";
Mpublic.	  Modifier ::= "public";
Mprivate.	  Modifier ::= "private";
Mstatic.	  Modifier ::= "static";
MEmpty.       Modifier ::= "" ;
terminator Modifier "";


-- statements ----------------------------------------------

BlockS.    Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Lhs "=" Expr  ";" ;

Incr.      Stmt ::= Lhs "++"  ";" ;

Decr.      Stmt ::= Lhs "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForArr.    Stmt ::= "for" "(" TypeName Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- LHS & RHS -----------------------------------------------

VariableRawLhs.     Lhs ::= Ident ;
ArrElemLhs.         Lhs ::= Ident "[" Expr "]" ;
FieldLhs.           Lhs ::= Expr "." Ident ;




-- Types ---------------------------------------------------

Int.       BasicType ::= "int" ;
Str.       BasicType ::= "string" ;
Bool.      BasicType ::= "boolean" ;
Void.      BasicType ::= "void" ;

BuiltIn.   TypeName ::= BasicType ;
ClassName. TypeName ::= Ident ;

ArrayType. Type ::= TypeName "[]" ;
TypeNameS. Type ::= TypeName ;

-- internal   Fun. Type ::= Type "(" [Type] ")" ;
separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr9 ::= Ident ;
ELitInt.   Expr9 ::= Integer ;
ELitTrue.  Expr9 ::= "true" ;
ELitFalse. Expr9 ::= "false" ;
EThis.     Expr9 ::= "self" ;
ENull.     Expr9 ::= "null" ;
EApp.      Expr9 ::= Ident "(" [Expr] ")" ;
EString.   Expr9 ::= String ;
EConstr.   Expr9 ::= "new" Ident ;

EArrConstr. Expr8 ::= "new" TypeName "[" Expr "]" ;
ENDArrAcc.  Expr8 ::= Expr9 "[" Expr "]" ;

EObjAcc.  Expr7 ::= Expr8 ObjAcc ;

ECast.     Expr6 ::= "(" TypeName ")" Expr7 ;

Neg.       Expr5 ::= "-" Expr6 ;
Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;
EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;
ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;
EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 9 ;

separator  Expr "," ;

-- object access ------------------------------------------

ObjFieldAcc. ObjAcc ::= "." Ident ;
ObjMethAcc.  ObjAcc ::= "." Ident "(" [Expr] ")" ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;


-- comments ------------------------------------------------

comment    "#" ;
comment    "//" ;
comment    "/*" "*/" ;
